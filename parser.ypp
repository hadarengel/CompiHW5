%{
    /* Prologue */
    #include <stdio.h>
    #include "hw3_output.hpp"
    #include "llvm_compiler.hpp"
    #include "parser.tab.hpp"

    extern int yylineno;
    extern int yylex();
    int yyerror(const char* message);
    Llvm_compiler comp;

    using namespace output;


%}

    /* Declerations Section */
 



    %token    B BOOL VOID INT BYTE STRING CONST TRUE FALSE IF RETURN WHILE BREAK CONTINUE SC COMMA NUM ID

    %right ASSIGN    

    %left OR
    %left AND
    %left EQUALOP
    %nonassoc RELOP 
    %left ADD SUB
    %left MULT DIV
	%right NOT IF
    %left LPAREN RPAREN LBRACE RBRACE
    %nonassoc ELSE


%%
    /* Grammer Rules Section */ 

    Program:  Funcs 
    ; 

    Funcs: /* empty */ {} 
    | FuncDecl Funcs {}
    ;

    FuncDecl: MFuncDecl LBRACE Statements RBRACE {comp.handle_func_end($1.type);}
    ;

    MFuncDecl: RetType ID LPAREN Formals RPAREN 
    {
        comp.handle_func_decl($1.type,$2.data);
        $$.type = $1.type;
    }
    ;
    RetType: Type {$$.type = $1.type;} 
    | VOID {$$.type = "VOID";}
    ;

    Formals: /* empty */ 
    | FormalsList 
    ;

    FormalsList: FormalDecl {comp.add_func_arg($1);}
    | FormalDecl COMMA FormalsList {comp.add_func_arg($1);}
    ;

    FormalDecl: TypeAnnotation Type ID 
        {
        $$.data = $3.data;
        $$.type = $2.type;
        $$.is_const = $1.is_const;
        }
    ;

    Statements: Statement {$$ = $1;}
    | Statements Statement {$$ = comp.merge_lists($1,$2);}
    ;

    Statement: LBRACE {comp.openScope();} Statements RBRACE {comp.closeScope(); $$ = $3;}
    | TypeAnnotation Type ID SC 
        {
        if($1.is_const) output::errorConstDef(yylineno);
        comp.handle_var_decl($2.type,$3.data);
        }
    | TypeAnnotation Type ID ASSIGN Exp SC  
        {
        comp.handle_var_decl($1.is_const,$2.type,$3.data,$5);
        }
    | ID ASSIGN Exp SC  
        {
        comp.handle_assign($1.data,$3);
        }
    | Call SC {}
    | RETURN SC {comp.handle_return();}
    | RETURN Exp SC {comp.handle_return($2);}
    | IF LPAREN MExp RPAREN  Statement {$$ = comp.end_if($3,$5);}
    | IF LPAREN MExp RPAREN  Statement MElse Statement {$$ = comp.end_else($3,$5,$6,$7);}
    | MWhile LPAREN MExp RPAREN Statement {comp.end_while($3,$5);}
    | BREAK SC {$$ = comp.handle_break();}
    | CONTINUE SC {comp.handle_continue();}
    ;

    MExp: Exp {$$ = comp.handle_cond($1);}
    ;

    MElse: ELSE {$$ = comp.begin_else();}
    ;

    MWhile: WHILE {comp.begin_while();}
    ;

    Call: ID LPAREN ExpList RPAREN {$$ = comp.handle_call($1.data);}
    | ID LPAREN RPAREN {$$ = comp.handle_call($1.data);}
    ;

    ExpList: Exp {comp.add_call_arg($1);}
    | Exp COMMA ExpList {comp.add_call_arg($1);}
    ;

    Type: INT {$$.type="INT";}
    | BYTE {$$.type="BYTE";}
    | BOOL {$$.type="BOOL";}
    ;

    TypeAnnotation: /* empty */ {$$.is_const = false;}
    | CONST {$$.is_const = true;}
    ; 

    Exp: LPAREN Exp RPAREN {$$ = $2;}
    | Exp ADD Exp  
        {
        $$ = comp.handle_add($1,$3);
        }
    | Exp SUB Exp  
        {
        $$ = comp.handle_sub($1,$3);
        }
    | Exp MULT Exp  
        {
        $$ = comp.handle_mul($1,$3);
        }
    | Exp DIV Exp  
        {
        $$ = comp.handle_div($1,$3);
        }
    | ID
        {
        $$ = comp.handle_var($1.data);
        }
    | Call 
        {
            if($1.type == "BOOL"){
                comp.add_br_and_label($1);
            }
            $$ = $1;
        }
    | NUM {$$ = comp.handle_literal($1.data,"INT");}
    | NUM B {$$ = comp.handle_literal($1.data,"BYTE");}
    | STRING {$$ = comp.handle_string($1.data);}
    | TRUE {$$.data = "1"; $$.type = "BOOL"; $$.is_literal = true;}
    | FALSE {$$.data = "0"; $$.type = "BOOL"; $$.is_literal = true;}
    | NOT Exp   
        {
        $$ = comp.handle_not($2);
        }
    | Exp AND {comp.add_br_and_label($1);} Exp {$$ = comp.handle_and($1,$4);}   
    | Exp OR {comp.add_br_and_label($1);} Exp {$$ = comp.handle_or($1,$4);}
    | Exp RELOP Exp 
        {
        $$ = comp.handle_relop_equalop($1,$2.data,$3);
        }
    | Exp EQUALOP Exp   
        {
        $$ = comp.handle_relop_equalop($1,$2.data,$3);
        }
    | LPAREN Type RPAREN Exp 
        {
        $$ = comp.handle_cast($2.type,$4);
        } 
    ;


    
%%
    /* Epilogue Section */    

    int main()
    {
        int parse_ret = yyparse();
        if(!comp.is_main_exist()){
            output::errorMainMissing();
            exit(1);
        }
        comp.printBuffers();
        return parse_ret;
    }

    int yyerror(const char* message){
        output::errorSyn(yylineno);
        exit(0);
    }
    